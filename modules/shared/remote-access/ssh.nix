{ config, universe, pkgs, lib, ... }:
let
  inherit (lib) types mkOption mkIf;
  secret-name = universe.config.remote-access.openssh.secret-name;
  inherit (pkgs) openssh;
  key-name = "id_ed25519";

  /**
  Script which generates a private/public openssh key pair. The private
  key will be encrypted using the nahual's master key before being
  saved to the store.

  A private/public key pair will be generated for each nahual and will
  become the ssh identity of the "tikal-user". The public key of all
  other nahuales will be added to the "authorized_keys" file, allowing
  the "tikal-user" from every nahual to ssh into the other nahuales.

  Todo: allow finer grain control on which nahual can access other
  nahuales.
  */
  gen-key-script =
    ''
    mkdir -p "$public"
    mkdir -p "$private"
    ${openssh}/bin/ssh-keygen -N "" -t ed25519 -f "$private/${key-name}"
    cp "$private/${key-name}.pub" "$public"
    ''
  ;
in
  {
    options = {
      tikal.openssh = {
        secret-name = mkOption {
          description = ''
            The name used to identify the tikal secrets generated for
            ssh purposes. The ssh keys generated by tikal will be used
            to authenticate ssh connections between nahuales.
          '';
          readOnly = true;
          type = types.str;
          default = secret-name;
        };

        key-name = mkOption {
          description = ''
            The name of the file which will contain the ssh key.
          '';
          readOnly = true;
          type = types.str;
          default = key-name;
        };
      };
    };
    config = mkIf universe.config.remote-access.openssh.enable {
      secrets.all-nahuales = {
        ${secret-name} = {
          text = gen-key-script;
          user = "tikal";
          group = "tikal";
        };
      };
    };
  }
