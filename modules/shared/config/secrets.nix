{ lib, config, ... }:
let
  inherit (lib) mkOption types;
  secret.options = {
    text = mkOption {
      type = types.str;
      description = ''
        This option is a bash program that generates the secret.
        This program will be executed in order to generate the
        secret and then the output will be encrypted using the
        nahual's public key. The following env variables will
        be available durring the execution of the program:
          - $nahual_name: The name of the nahual for which the
            secret is being generated.
          - $out: The output directory. Note that this will
            contain two important directories:
            - private: "$out/private"
            - public: "$out/public"
        The program should place all secrets in the 
        private directory as this will get encrypted
        by the nahual's key before landing on the
        Nix store. The public directory can be used
        to save any public data that might be generated
        alongside the secret (ie. public keys).
      '';
    };
    user = mkOption {
      type = types.nullOr types.str;
      description = ''
        The ownership that the decrypted secret will have on the
        running NixOs system.
      '';
      default = null;
    };
    group = mkOption {
      type = types.nullOr types.str;
      description = ''
        The group that will be assigned to the folder containing
        the decrypted system on the running NixOs system.
      '';
      default = null;
    };
  };
  # Only add values to the config if there
  # are any secrets that need to be generated
  # and locked
  is-enabled = lib.length (lib.attrNames config.secrets.all-nahuales) > 0;
in
  {
    options.secrets = {

      enable = mkOption {
        type = types.bool;
        default = is-enabled;
        readOnly = true;
        description = ''
          This module is responsible for generating the sync scripts that
          responsible for embedding secrets in nahuales. Secrets are
          generated by running a script and then encrypted before being
          saved to the nixos store. These derivations are often not
          stable, so Tikal also offers as mechanism (called store-lock)
          which allows pinning specific deriviations in a repository.
          The secrets module relies on the "store-lock" module to
          pin these secrets, which is done by the sync script.
        '';
      };

      all-nahuales = mkOption {
        type = types.attrsOf (types.submodule secret);
        description = ''
          A secret that will be generated for all the nahuales
          in the tikal universe. Note that the secret will be
          unique per nahual, but the same script will be used
          to derive each of the individual secrets.
        '';
        default = {};
      };
    };
  }
