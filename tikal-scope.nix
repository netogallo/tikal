{
  nixpkgs,
  lib,
  universe,
  flake-root,
  base-dir ? null,
  universe-repository ? null,
  log-level ? 1,
  test-filters ? null,
  ...
}:
let
  tikal-config =
    {
      inherit base-dir log-level test-filters flake-root
        universe-repository;
      nixos-version = "25.11";
    }
  ;

  # The base scope only contains the core tikal libraries
  # which don't require pkgs. This is mainly created
  # to have a logger.
  base-scope = lib.makeScope lib.callPackageWith (self: {
    inherit nixpkgs tikal-config lib;
    tikal = {
      prelude = self.callPackage ./lib/prelude.nix {};
      hardcoded = self.callPackage ./lib/hardcoded.nix {};
    };
  });

  log = base-scope.tikal.prelude.log.add-context { file = ./tikal-scope.nix; };

  # This is the context used to evaluate the tikal universe.
  # This context is narrower than the context that will be
  # used to evaluate the sync script as it is meant to be
  # platform agnostic (ie. no pkgs).
  universe-scope = base-scope.overrideScope (self: super:
  {
    universe-spec = universe;
    shared-context =
      log.log-function-call "shared-context" self.callPackage ./lib/universe/shared-context.nix { };
    flake-context =
      log.log-function-call "flake-context" self.callPackage ./lib/universe/flake-context.nix {};
    sync-context =
      log.log-function-call "sync-context" self.callPackage ./lib/universe/sync-context.nix { };
    universe = log.log-value "universe" (lib.evalModules {
      modules = [
        self.universe-spec
        ./modules/universe/main.nix
        {
          config._module.args = log.log-function-call "universe-eval-context" self.callPackage ./lib/modules/universe/main.nix { };
        }
      ];
    });
  });

  # The full scope will be available when evaluating the module for
  # the "sync" script, as well as the nixos modules that will be
  # generated by tikal. This module is platform-dependent. Therefore
  # it offers much more functionality which depends on pkgs.
  full-scope = { pkgs, nixos-rockchip }:
    let
      tikal-scopes = {
        full-scope = { pkgs }: full-scope { inherit pkgs nixos-rockchip; };
      };
    in
      lib.makeScope pkgs.newScope (self: {
        inherit tikal-scopes;
        inherit (universe-scope) universe nixpkgs shared-context flake-context sync-context;
        tikal-config = log.log-value "tikal-config" tikal-config;
        tikal = {
          prelude = self.callPackage ./lib/prelude.nix {};
          hardcoded = self.callPackage ./lib/hardcoded.nix {};
          xonsh = self.callPackage ./lib/xonsh.nix {};
          sync = self.callPackage ./lib/sync/lib.nix {};
          store = self.callPackage ./lib/store.nix {};
          syslog = self.callPackage ./lib/syslog.nix {};
          template = self.callPackage ./lib/template.nix {};
          platforms = self.callPackage ./lib/platforms.nix { inherit nixos-rockchip; };
          test-utils = self.callPackage ./lib/test-utils.nix { inherit universe-scope; };
        };
      }
    )
  ;
in
{
  inherit universe-scope full-scope;
}
