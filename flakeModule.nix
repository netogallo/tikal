{ tikal-flake, ... }: { self, lib, config, flake-parts-lib, ... }:
let
  inherit (tikal-flake.inputs) nixpkgs nixos-rockchip
    nix-crypto;
  inherit (flake-parts-lib)
    mkPerSystemOption;
  inherit (lib) mkOption types;
  config-any = config;

  call-tikal-package = module: context:
    lib.callPackageWith
    {
      inherit lib nixpkgs nixos-rockchip nix-crypto;
    }
    module
    (config.tikal // context)
  ;

  tikal-nixos-main = call-tikal-package ./nixos-main.nix {};
  glob-type = types.submodule {
    options = {
      glob = lib.mkOption {
        type = types.str;
        description = "The glob expression as a string";
      };
    };
  };
in
{
  config.flake = {
    inherit (tikal-nixos-main) nixosModules;
  };
  options = {
    tikal = {
      base-dir = lib.mkOption {
        description = ''
          The directory where the ".tikal" configuration directory
          is to be placed. By default it gets placed in the root
          of the flake. This must be a relative path.
        '';
        default = null;
        type = types.nullOr types.str;
      };

      universe-repository = lib.mkOption {
        description = ''
          The git repository which holds the universe definition. In general,
          it is the repository of the flake which defines the universe.
          The main purpose of this option is to embed the repository in
          the generated nixos images.
        '';
        default = null;
        type = types.nullOr types.str;
      };

      flake-root = lib.mkOption {
        description = ''
          The root path where the flake which defines the universe is located.
          The main use of this option is to determine where the files generated
          by the "sync" script are to be found. Note that this must be a store
          path and all public files generated by the "sync" are expected to reside
          under this path.
        '';
        type = types.path;
      };

      log-level = lib.mkOption {
        description = ''
          The log verbosity of the output when evaluating the tikal code. Normally,
          this should be left unchanged but can be used when modifying the tikal
          code for debugging. The loglevels are as follows:
            * debug-verbose = 8
            * debug = 7
            * info = 6
            * warning = 5
            * error = 4
            * no logs = < 4
        '';
        type = types.enum [ 0 1 2 3 4 5 6 7 8 ];
        default = 0;
      };
      test-filters = lib.mkOption {
        description = ''
          The tests for Tikal can run automatically as tikal code gets evaluated. This allows
          developers to easily asess if new changes break any tests. However, this can
          incurr a performance penalty, therefore a filter can be used to determine what
          tests are to be run. The filter is a "glob" string and the test will run if
          the name of the test matches any glob. This means that:
            test-filters = [ "*" ]
          implies that all tests will run every time Tika's nix code gets evaluated.
        '';
        default = [];
        type = types.listOf (types.oneOf [ types.str glob-type ]);
      };

      universe = lib.mkOption {
        description = ''
          This option allows the user to describe the general topology of the universe
          that will be created by Tikal. A universe is a set of Nahuales which are
          specs (nixos modules) that describe machines which are related to each other.
          Tikal provides various mechanisms that allow theese machines to network with
          each other. Furthermore, Tikal also takes care of generating cryptographic
          assets which are used by the machines within the universe to identify each
          other.
          ''
        ;
        # Todo: This will get validated by the module which resides in
        # "modules/universe/main.nix". For the time being, it will be done as an
        # extra step. Refactoring this might be a good idea.
        type = types.anything;
        default = {};
      };
    };

    perSystem = mkPerSystemOption ({ pkgs, system, ... }:
    let
      sync = call-tikal-package ./sync-main.nix { inherit pkgs system; };
      vms = tikal-nixos-main.vms { inherit pkgs; };
      installer = call-tikal-package ./install-main.nix { inherit pkgs system; };
      get-config-attrs = { packages ? {}, apps ? {}, ... }:
        { inherit packages apps; }
      ;
    in
    {
      config = {
        packages =
          sync.packages //
          vms.packages //
          installer.packages
        ;
        apps =
          sync.apps //
          vms.apps
        ;
      };
    });
  };
}

